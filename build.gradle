plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id 'maven-publish'
	id 'io.freefair.lombok' version '8.13'
	id 'com.modrinth.minotaur' version '2.+'
}

version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url = "https://moehreag.duckdns.org/maven/releases" }
	maven { url = "https://maven.parchmentmc.org" }
	maven { url = "https://maven.terraformersmc.com/releases" }
}

loom {
	mods {
		"random-world-names" {
			sourceSet sourceSets.main
		}
	}
	accessWidenerPath.set(file("src/main/resources/randomworldnames.accesswidener"))
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings(loom.layered {
		officialMojangMappings {
			nameSyntheticMembers = true
		}
		parchment("org.parchmentmc.data:parchment-${project.parchment_minecraft}:${project.parchment}@zip")
	})
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation(include("io.github.axolotlclient:AxolotlClient-config:3.0.7+1.21.5"))
	modImplementation(include("io.github.axolotlclient.AxolotlClient-config:AxolotlClientConfig-common:3.0.7"))

	modLocalRuntime("com.terraformersmc:modmenu:14.0.0-rc.2")
	
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {

	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "TE51HZna"
	versionType = "release"
	uploadFile = remapJar
	additionalFiles = [sourcesJar]
	syncBodyFrom = file("README.md").text
}